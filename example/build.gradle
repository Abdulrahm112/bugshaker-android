buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.github.stkent.bugshaker"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 2
        versionName "1.1.0"
    }

    signingConfigs {
        release {
            String releaseKeyStoreFileName = System.properties['user.home'] + "/.bugshaker/bugshaker.jks"
            String releasePropFileName = System.properties['user.home'] + "/.bugshaker/bugshaker.properties"

            if (file(releaseKeyStoreFileName).exists() && file(releasePropFileName).exists()) {
                Properties releaseProps = new Properties()
                releaseProps.load(new FileInputStream(releasePropFileName))

                storeFile file(releaseKeyStoreFileName)
                storePassword releaseProps.pass
                keyAlias releaseProps.alias
                keyPassword releaseProps.pass
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
            ext.enableCrashlytics = true
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile project(':bugshaker')

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
