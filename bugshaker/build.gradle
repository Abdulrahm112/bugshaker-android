// Shared project metadata

ext {
    // Library info:
    libraryVersion = '1.4.0'
    libraryName = 'bugshaker'
    libraryGroupId = 'com.github.stkent'
    libraryArtifactId = 'bugshaker'
    libraryDescription = 'Shake to send a bug report!'
    libraryInceptionYear = '2016'
    libraryGitHubRepoName = 'stkent/bugshaker-android'
    libraryGitHubUrl = "https://github.com/${libraryGitHubRepoName}"
    libraryIssueTrackerUrl = "${libraryGitHubUrl}/issues"
    libraryWebsiteUrl = libraryGitHubUrl
    libraryLabels = ['android']
    libraryLicenseName = 'Apache License Version 2.0'
    libraryLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.html'

    // Developer info:
    developerId = 'stkent'
    developerName = 'Stuart Kent'
    developerEmail = 'skent.dev@gmail.com'

    // Bintray categorization information:
    bintrayRepo = 'android-libraries'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.btkelly:gnag:1.3.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'gnag'

apply from: 'https://raw.githubusercontent.com/stkent/JCenter/master/bintray1.7.3.gradle'
apply from: 'https://raw.githubusercontent.com/stkent/JCenter/master/install1.5.gradle'

configurations {
    javadocDeps
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName libraryVersion
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard/rxjava.pro', 'proguard/bugshaker.pro', 'proguard/v7support.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    def supportLibraryVersion = '25.3.1'
    def seismicVersion = '1.0.2'

    javadocDeps "com.android.support:support-annotations:${supportLibraryVersion}"
    javadocDeps "com.squareup:seismic:${seismicVersion}"

    compile "com.squareup:seismic:${seismicVersion}"
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.9'

    compile('com.google.android.gms:play-services-maps:10.2.1') { ext.optional = true }
    compile("com.android.support:appcompat-v7:${supportLibraryVersion}") { ext.optional = true }

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.14'
}

gnag {
    failOnError true

    github {
        repoName 'stkent/bugshaker-android'
    }

    checkstyle {
        reporterConfig project.file('config/checkstyle.xml')
    }

    findbugs {
        reporterConfig project.file('config/findbugs.xml')
    }

    pmd {
        reporterConfig project.file('config/pmd.xml')
    }

    androidLint {
        enabled true
        severity 'Error'
    }
}

install {
    repositories.mavenInstaller {
        pom {
            // See comment by Endre St√∏lsvik on https://issues.gradle.org/browse/GRADLE-1749.
            withXml {
                def depMap = configurations.compile.dependencies.collectEntries { [it.name, it] }
                asNode().dependencies.dependency.findAll {
                    def dep = depMap.get(it.artifactId.text())
                    return dep?.hasProperty('optional') && dep.optional
                }.each {
                    if (it.optional) {
                        it.optional.value = 'true'
                    } else {
                        it.appendNode('optional', 'true')
                    }
                }
            }
        }
    }
}
